buildscript {
    ext.kotlin_version = '1.5.31'
    ext.ktor_version = '1.6.3'
    ext.exposed_version = '0.35.1'
    ext.restAssured_version = '4.4.0'
    ext.klogging_version = '2.0.11'
    ext.libVersion = '6.0.0-beta-1'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.18.1"
    }
}

group = 'io.github.adven27'
version = libVersion

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }

    group = 'io.github.adven27'
    version = libVersion

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'utf-8'
    compileTestJava.options.encoding = 'utf-8'

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    test {
        systemProperty "user.timezone", "Europe/Moscow"
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = 'full'
        testLogging.events('FAILED', 'PASSED')
        maxParallelForks = Runtime.runtime.availableProcessors()
    }

    apply from: "$rootDir/gradle/linters.gradle"
}

task publishMaven {
    dependsOn ":exam-core:publish"
    dependsOn ":exam-db:publish"
    dependsOn ":exam-ws:publish"
    dependsOn ":exam-mq:publish"
    dependsOn ":exam-mq-rabbit:publish"
    dependsOn ":exam-mq-kafka:publish"
    dependsOn ":exam-mq-ibmmq:publish"
    dependsOn ":exam-mq-redis:publish"
    dependsOn ":exam-files:publish"
    dependsOn ":exam-ui:publish"
    dependsOn ":exam-ms:publish"
}
